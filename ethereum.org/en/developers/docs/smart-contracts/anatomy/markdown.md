Skip to main content

[](/en/)

  * Learn
  * Use
  * Build
  * Participate
  * Research

Search```K`

Languages EN

Change page

[Overview](/en/developers/docs/)

Foundational topics

[Intro to Ethereum](/en/developers/docs/intro-to-ethereum/)

[Intro to Ether](/en/developers/docs/intro-to-ether/)

[Intro to dapps](/en/developers/docs/dapps/)

[Web2 vs Web3](/en/developers/docs/web2-vs-web3/)

[Accounts](/en/developers/docs/accounts/)

[Transactions](/en/developers/docs/transactions/)

[Blocks](/en/developers/docs/blocks/)

[Ethereum virtual machine (EVM)](/en/developers/docs/evm/)

[Opcodes](/en/developers/docs/evm/opcodes/)

[Gas](/en/developers/docs/gas/)

[Nodes and clients](/en/developers/docs/nodes-and-clients/)

[Run a node](/en/developers/docs/nodes-and-clients/run-a-node/)

[Client diversity](/en/developers/docs/nodes-and-clients/client-diversity/)

[Nodes as a service](/en/developers/docs/nodes-and-clients/nodes-as-a-
service/)

[Node architecture](/en/developers/docs/nodes-and-clients/node-architecture/)

[Light clients](/en/developers/docs/nodes-and-clients/light-clients/)

[Archive nodes](/en/developers/docs/nodes-and-clients/archive-nodes/)

[Bootnodes](/en/developers/docs/nodes-and-clients/bootnodes/)

[Networks](/en/developers/docs/networks/)

[Consensus mechanisms](/en/developers/docs/consensus-mechanisms/)

[Proof-of-work](/en/developers/docs/consensus-mechanisms/pow/)

[Mining](/en/developers/docs/consensus-mechanisms/pow/mining/)

[Mining algorithms](/en/developers/docs/consensus-
mechanisms/pow/mining/mining-algorithms/)

[Dagger-Hashimoto](/en/developers/docs/consensus-mechanisms/pow/mining/mining-
algorithms/dagger-hashimoto/)

[Ethash](/en/developers/docs/consensus-mechanisms/pow/mining/mining-
algorithms/ethash/)

[Proof-of-stake](/en/developers/docs/consensus-mechanisms/pos/)

[Gasper](/en/developers/docs/consensus-mechanisms/pos/gasper/)

[Weak subjectivity](/en/developers/docs/consensus-mechanisms/pos/weak-
subjectivity/)

[Attestations](/en/developers/docs/consensus-mechanisms/pos/attestations/)

[PoS rewards and penalties](/en/developers/docs/consensus-
mechanisms/pos/rewards-and-penalties/)

[PoS attack and defense](/en/developers/docs/consensus-mechanisms/pos/attack-
and-defense/)

[Keys](/en/developers/docs/consensus-mechanisms/pos/keys/)

[Proof-of-stake versus proof-of-work](/en/developers/docs/consensus-
mechanisms/pos/pos-vs-pow/)

[Block proposal](/en/developers/docs/consensus-mechanisms/pos/block-proposal/)

[Proof-of-stake FAQs](/en/developers/docs/consensus-mechanisms/pos/faqs/)

[Proof-of-authority](/en/developers/docs/consensus-mechanisms/poa/)

Ethereum stack

[Intro to the stack](/en/developers/docs/ethereum-stack/)

[Smart contracts](/en/developers/docs/smart-contracts/)

[Smart contract languages](/en/developers/docs/smart-contracts/languages/)

[Smart contract anatomy](/en/developers/docs/smart-contracts/anatomy/)

[Smart contracts libraries](/en/developers/docs/smart-contracts/libraries/)

[Testing smart contracts](/en/developers/docs/smart-contracts/testing/)

[Compiling smart contracts](/en/developers/docs/smart-contracts/compiling/)

[Deploying smart contracts](/en/developers/docs/smart-contracts/deploying/)

[Verifying smart contracts](/en/developers/docs/smart-contracts/verifying/)

[Upgrading smart contracts](/en/developers/docs/smart-contracts/upgrading/)

[Smart contract security](/en/developers/docs/smart-contracts/security/)

[Smart contract formal verification](/en/developers/docs/smart-
contracts/formal-verification/)

[Composability](/en/developers/docs/smart-contracts/composability/)

[Development networks](/en/developers/docs/development-networks/)

[Development frameworks](/en/developers/docs/frameworks/)

Ethereum client APIs

[JavaScript APIs](/en/developers/docs/apis/javascript/)

[Backend APIs](/en/developers/docs/apis/backend/)

[JSON-RPC](/en/developers/docs/apis/json-rpc/)

[Data and analytics](/en/developers/docs/data-and-analytics/)

[Block explorers](/en/developers/docs/data-and-analytics/block-explorers/)

[Storage](/en/developers/docs/storage/)

[Integrated Development Environments (IDEs)](/en/developers/docs/ides/)

[Programming languages](/en/developers/docs/programming-languages/)

[Dart](/en/developers/docs/programming-languages/dart/)

[Delphi](/en/developers/docs/programming-languages/delphi/)

[.NET](/en/developers/docs/programming-languages/dot-net/)

[Golang](/en/developers/docs/programming-languages/golang/)

[Java](/en/developers/docs/programming-languages/java/)

[JavaScript](/en/developers/docs/programming-languages/javascript/)

[Python](/en/developers/docs/programming-languages/python/)

[Ruby](/en/developers/docs/programming-languages/ruby/)

[Rust](/en/developers/docs/programming-languages/rust/)

Advanced

[Bridges](/en/developers/docs/bridges/)

[Standards](/en/developers/docs/standards/)

[Token standards](/en/developers/docs/standards/tokens/)

[ERC-20: Fungible Tokens](/en/developers/docs/standards/tokens/erc-20/)

[ERC-721: NFTs](/en/developers/docs/standards/tokens/erc-721/)

[ERC-777](/en/developers/docs/standards/tokens/erc-777/)

[ERC-1155](/en/developers/docs/standards/tokens/erc-1155/)

[ERC-4626](/en/developers/docs/standards/tokens/erc-4626/)

[Maximal extractable value (MEV)](/en/developers/docs/mev/)

[Oracles](/en/developers/docs/oracles/)

[Scaling](/en/developers/docs/scaling/)

[Optimistic rollups](/en/developers/docs/scaling/optimistic-rollups/)

[Zero-knowledge rollups](/en/developers/docs/scaling/zk-rollups/)

[State channels](/en/developers/docs/scaling/state-channels/)

[Sidechains](/en/developers/docs/scaling/sidechains/)

[Plasma](/en/developers/docs/scaling/plasma/)

[Validium](/en/developers/docs/scaling/validium/)

[Data availability](/en/developers/docs/data-availability/)

[Networking layer](/en/developers/docs/networking-layer/)

[Network addresses](/en/developers/docs/networking-layer/network-addresses/)

[Portal Network](/en/developers/docs/networking-layer/portal-network/)

[Data structures and encoding](/en/developers/docs/data-structures-and-
encoding/)

[Patricia Merkle Trie](/en/developers/docs/data-structures-and-
encoding/patricia-merkle-trie/)

[Recursive-length prefix (RLP)](/en/developers/docs/data-structures-and-
encoding/rlp/)

[Simple serialize (SSZ)](/en/developers/docs/data-structures-and-
encoding/ssz/)

[Web3 secret storage definition](/en/developers/docs/data-structures-and-
encoding/web3-secret-storage/)

Design fundamentals

[Intro to design and UX](/en/developers/docs/design-and-ux/)

# Anatomy of smart contracts

c

Last edit: [@corwintines(opens in a new tab)](https://github.com/corwintines),
November 19, 2023

See contributors

On this page

  * Prerequisites
  * Data
    * Storage
    * Memory
    * Environment variables
  * Functions
    * View functions
    * Constructor functions
    * Built-in functions
  * Writing functions
  * Events and logs
  * Annotated examples
    * Hello world
    * Token
    * Unique digital asset
  * Further reading
  * Related topics
  * Related tutorials

A smart contract is a program that runs at an address on Ethereum. They're
made up of data and functions that can execute upon receiving a transaction.
Here's an overview of what makes up a smart contract.

## Prerequisites

Make sure you've read about [smart contracts](/en/developers/docs/smart-
contracts/) first. This document assumes you're already familiar with
programming languages such as JavaScript or Python.

## Data

Any contract data must be assigned to a location: either to `storage` or
`memory`. It's costly to modify storage in a smart contract so you need to
consider where your data should live.

### Storage

Persistent data is referred to as storage and is represented by state
variables. These values get stored permanently on the blockchain. You need to
declare the type so that the contract can keep track of how much storage on
the blockchain it needs when it compiles.

    
    
    1// Solidity example
    
    2contract SimpleStorage {
    
    3    uint storedData; // State variable
    
    4    // ...
    
    5}
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy
    
    
    1# Vyper example
    
    2storedData: int128
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy

If you've already programmed object-oriented languages, you'll likely be
familiar with most types. However `address` should be new to you if you're new
to Ethereum development.

An `address` type can hold an Ethereum address which equates to 20 bytes or
160 bits. It returns in hexadecimal notation with a leading 0x.

Other types include:

  * boolean
  * integer
  * fixed point numbers
  * fixed-size byte arrays
  * dynamically-sized byte arrays
  * Rational and integer literals
  * String literals
  * Hexadecimal literals
  * Enums

For more explanation, take a look at the docs:

  * [See Vyper types(opens in a new tab)](https://vyper.readthedocs.io/en/v0.1.0-beta.6/types.html#value-types)
  * [See Solidity types(opens in a new tab)](https://solidity.readthedocs.io/en/latest/types.html#value-types)

### Memory

Values that are only stored for the lifetime of a contract function's
execution are called memory variables. Since these are not stored permanently
on the blockchain, they are much cheaper to use.

Learn more about how the EVM stores data (Storage, Memory, and the Stack) in
the [Solidity docs(opens in a new
tab)](https://solidity.readthedocs.io/en/latest/introduction-to-smart-
contracts.html?highlight=memory#storage-memory-and-the-stack).

### Environment variables

In addition to the variables you define on your contract, there are some
special global variables. They are primarily used to provide information about
the blockchain or current transaction.

Examples:

**Prop**| **State variable**| **Description**  
---|---|---  
`block.timestamp`| uint256| Current block epoch timestamp  
`msg.sender`| address| Sender of the message (current call)  
  
## Functions

In the most simplistic terms, functions can get information or set information
in response to incoming transactions.

There are two types of function calls:

  * `internal` â€“ these don't create an EVM call
    * Internal functions and state variables can only be accessed internally (i.e. from within the current contract or contracts deriving from it)
  * `external` â€“ these do create an EVM call
    * External functions are part of the contract interface, which means they can be called from other contracts and via transactions. An external function `f` cannot be called internally (i.e. `f()` does not work, but `this.f()` works).

They can also be `public` or `private`

  * `public` functions can be called internally from within the contract or externally via messages
  * `private` functions are only visible for the contract they are defined in and not in derived contracts

Both functions and state variables can be made public or private

Here's a function for updating a state variable on a contract:

    
    
    1// Solidity example
    
    2function update_name(string value) public {
    
    3    dapp_name = value;
    
    4}
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy

  * The parameter `value` of type `string` is passed into the function: `update_name`
  * It's declared `public`, meaning anyone can access it
  * It's not declared `view`, so it can modify the contract state

### View functions

These functions promise not to modify the state of the contract's data. Common
examples are "getter" functions â€“ you might use this to receive a user's
balance for example.

    
    
    1// Solidity example
    
    2function balanceOf(address _owner) public view returns (uint256 _balance) {
    
    3    return ownerPizzaCount[_owner];
    
    4}
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy
    
    
    1dappName: public(string)
    
    2
    
    3@view
    
    4@public
    
    5def readName() -> string:
    
    6  return dappName
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy

What is considered modifying state:

  1. Writing to state variables.
  2. [Emitting events(opens in a new tab)](https://solidity.readthedocs.io/en/v0.7.0/contracts.html#events).
  3. [Creating other contracts(opens in a new tab)](https://solidity.readthedocs.io/en/v0.7.0/control-structures.html#creating-contracts).
  4. Using `selfdestruct`.
  5. Sending ether via calls.
  6. Calling any function not marked `view` or `pure`.
  7. Using low-level calls.
  8. Using inline assembly that contains certain opcodes.

### Constructor functions

`constructor` functions are only executed once when the contract is first
deployed. Like `constructor` in many class-based programming languages, these
functions often initialize state variables to their specified values.

    
    
    1// Solidity example
    
    2// Initializes the contract's data, setting the `owner`
    
    3// to the address of the contract creator.
    
    4constructor() public {
    
    5    // All smart contracts rely on external transactions to trigger its functions.
    
    6    // `msg` is a global variable that includes relevant data on the given transaction,
    
    7    // such as the address of the sender and the ETH value included in the transaction.
    
    8    // Learn more: https://solidity.readthedocs.io/en/v0.5.10/units-and-global-variables.html#block-and-transaction-properties
    
    9    owner = msg.sender;
    
    10}
    
    Show all
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy
    
    
    1# Vyper example
    
    2
    
    3@external
    
    4def __init__(_beneficiary: address, _bidding_time: uint256):
    
    5    self.beneficiary = _beneficiary
    
    6    self.auctionStart = block.timestamp
    
    7    self.auctionEnd = self.auctionStart + _bidding_time
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy

### Built-in functions

In addition to the variables and functions you define on your contract, there
are some special built-in functions. The most obvious example is:

  * `address.send()` â€“ Solidity
  * `send(address)` â€“ Vyper

These allow contracts to send ETH to other accounts.

## Writing functions

Your function needs:

  * parameter variable and type (if it accepts parameters)
  * declaration of internal/external
  * declaration of pure/view/payable
  * returns type (if it returns a value)

    
    
    1pragma solidity >=0.4.0 <=0.6.0;
    
    2
    
    3contract ExampleDapp {
    
    4    string dapp_name; // state variable
    
    5
    
    6    // Called when the contract is deployed and initializes the value
    
    7    constructor() public {
    
    8        dapp_name = "My Example dapp";
    
    9    }
    
    10
    
    11    // Get Function
    
    12    function read_name() public view returns(string) {
    
    13        return dapp_name;
    
    14    }
    
    15
    
    16    // Set Function
    
    17    function update_name(string value) public {
    
    18        dapp_name = value;
    
    19    }
    
    20}
    
    Show all
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy

A complete contract might look something like this. Here the `constructor`
function provides an initial value for the `dapp_name` variable.

## Events and logs

Events let you communicate with your smart contract from your frontend or
other subscribing applications. When a transaction is mined, smart contracts
can emit events and write logs to the blockchain that the frontend can then
process.

## Annotated examples

These are some examples written in Solidity. If you'd like to play with the
code, you can interact with them in [Remix(opens in a new
tab)](http://remix.ethereum.org).

### Hello world

    
    
    1// Specifies the version of Solidity, using semantic versioning.
    
    2// Learn more: https://solidity.readthedocs.io/en/v0.5.10/layout-of-source-files.html#pragma
    
    3pragma solidity ^0.5.10;
    
    4
    
    5// Defines a contract named `HelloWorld`.
    
    6// A contract is a collection of functions and data (its state).
    
    7// Once deployed, a contract resides at a specific address on the Ethereum blockchain.
    
    8// Learn more: https://solidity.readthedocs.io/en/v0.5.10/structure-of-a-contract.html
    
    9contract HelloWorld {
    
    10
    
    11    // Declares a state variable `message` of type `string`.
    
    12    // State variables are variables whose values are permanently stored in contract storage.
    
    13    // The keyword `public` makes variables accessible from outside a contract
    
    14    // and creates a function that other contracts or clients can call to access the value.
    
    15    string public message;
    
    16
    
    17    // Similar to many class-based object-oriented languages, a constructor is
    
    18    // a special function that is only executed upon contract creation.
    
    19    // Constructors are used to initialize the contract's data.
    
    20    // Learn more: https://solidity.readthedocs.io/en/v0.5.10/contracts.html#constructors
    
    21    constructor(string memory initMessage) public {
    
    22        // Accepts a string argument `initMessage` and sets the value
    
    23        // into the contract's `message` storage variable).
    
    24        message = initMessage;
    
    25    }
    
    26
    
    27    // A public function that accepts a string argument
    
    28    // and updates the `message` storage variable.
    
    29    function update(string memory newMessage) public {
    
    30        message = newMessage;
    
    31    }
    
    32}
    
    Show all
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy

### Token

    
    
    1pragma solidity ^0.5.10;
    
    2
    
    3contract Token {
    
    4    // An `address` is comparable to an email address - it's used to identify an account on Ethereum.
    
    5    // Addresses can represent a smart contract or an external (user) accounts.
    
    6    // Learn more: https://solidity.readthedocs.io/en/v0.5.10/types.html#address
    
    7    address public owner;
    
    8
    
    9    // A `mapping` is essentially a hash table data structure.
    
    10    // This `mapping` assigns an unsigned integer (the token balance) to an address (the token holder).
    
    11    // Learn more: https://solidity.readthedocs.io/en/v0.5.10/types.html#mapping-types
    
    12    mapping (address => uint) public balances;
    
    13
    
    14    // Events allow for logging of activity on the blockchain.
    
    15    // Ethereum clients can listen for events in order to react to contract state changes.
    
    16    // Learn more: https://solidity.readthedocs.io/en/v0.5.10/contracts.html#events
    
    17    event Transfer(address from, address to, uint amount);
    
    18
    
    19    // Initializes the contract's data, setting the `owner`
    
    20    // to the address of the contract creator.
    
    21    constructor() public {
    
    22        // All smart contracts rely on external transactions to trigger its functions.
    
    23        // `msg` is a global variable that includes relevant data on the given transaction,
    
    24        // such as the address of the sender and the ETH value included in the transaction.
    
    25        // Learn more: https://solidity.readthedocs.io/en/v0.5.10/units-and-global-variables.html#block-and-transaction-properties
    
    26        owner = msg.sender;
    
    27    }
    
    28
    
    29    // Creates an amount of new tokens and sends them to an address.
    
    30    function mint(address receiver, uint amount) public {
    
    31        // `require` is a control structure used to enforce certain conditions.
    
    32        // If a `require` statement evaluates to `false`, an exception is triggered,
    
    33        // which reverts all changes made to the state during the current call.
    
    34        // Learn more: https://solidity.readthedocs.io/en/v0.5.10/control-structures.html#error-handling-assert-require-revert-and-exceptions
    
    35
    
    36        // Only the contract owner can call this function
    
    37        require(msg.sender == owner, "You are not the owner.");
    
    38
    
    39        // Enforces a maximum amount of tokens
    
    40        require(amount < 1e60, "Maximum issuance exceeded");
    
    41
    
    42        // Increases the balance of `receiver` by `amount`
    
    43        balances[receiver] += amount;
    
    44    }
    
    45
    
    46    // Sends an amount of existing tokens from any caller to an address.
    
    47    function transfer(address receiver, uint amount) public {
    
    48        // The sender must have enough tokens to send
    
    49        require(amount <= balances[msg.sender], "Insufficient balance.");
    
    50
    
    51        // Adjusts token balances of the two addresses
    
    52        balances[msg.sender] -= amount;
    
    53        balances[receiver] += amount;
    
    54
    
    55        // Emits the event defined earlier
    
    56        emit Transfer(msg.sender, receiver, amount);
    
    57    }
    
    58}
    
    Show all
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy

### Unique digital asset

    
    
    1pragma solidity ^0.5.10;
    
    2
    
    3// Imports symbols from other files into the current contract.
    
    4// In this case, a series of helper contracts from OpenZeppelin.
    
    5// Learn more: https://solidity.readthedocs.io/en/v0.5.10/layout-of-source-files.html#importing-other-source-files
    
    6
    
    7import "../node_modules/@openzeppelin/contracts/token/ERC721/IERC721.sol";
    
    8import "../node_modules/@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";
    
    9import "../node_modules/@openzeppelin/contracts/introspection/ERC165.sol";
    
    10import "../node_modules/@openzeppelin/contracts/math/SafeMath.sol";
    
    11
    
    12// The `is` keyword is used to inherit functions and keywords from external contracts.
    
    13// In this case, `CryptoPizza` inherits from the `IERC721` and `ERC165` contracts.
    
    14// Learn more: https://solidity.readthedocs.io/en/v0.5.10/contracts.html#inheritance
    
    15contract CryptoPizza is IERC721, ERC165 {
    
    16    // Uses OpenZeppelin's SafeMath library to perform arithmetic operations safely.
    
    17    // Learn more: https://docs.openzeppelin.com/contracts/2.x/api/math#SafeMath
    
    18    using SafeMath for uint256;
    
    19
    
    20    // Constant state variables in Solidity are similar to other languages
    
    21    // but you must assign from an expression which is constant at compile time.
    
    22    // Learn more: https://solidity.readthedocs.io/en/v0.5.10/contracts.html#constant-state-variables
    
    23    uint256 constant dnaDigits = 10;
    
    24    uint256 constant dnaModulus = 10 ** dnaDigits;
    
    25    bytes4 private constant _ERC721_RECEIVED = 0x150b7a02;
    
    26
    
    27    // Struct types let you define your own type
    
    28    // Learn more: https://solidity.readthedocs.io/en/v0.5.10/types.html#structs
    
    29    struct Pizza {
    
    30        string name;
    
    31        uint256 dna;
    
    32    }
    
    33
    
    34    // Creates an empty array of Pizza structs
    
    35    Pizza[] public pizzas;
    
    36
    
    37    // Mapping from pizza ID to its owner's address
    
    38    mapping(uint256 => address) public pizzaToOwner;
    
    39
    
    40    // Mapping from owner's address to number of owned token
    
    41    mapping(address => uint256) public ownerPizzaCount;
    
    42
    
    43    // Mapping from token ID to approved address
    
    44    mapping(uint256 => address) pizzaApprovals;
    
    45
    
    46    // You can nest mappings, this example maps owner to operator approvals
    
    47    mapping(address => mapping(address => bool)) private operatorApprovals;
    
    48
    
    49    // Internal function to create a random Pizza from string (name) and DNA
    
    50    function _createPizza(string memory _name, uint256 _dna)
    
    51        // The `internal` keyword means this function is only visible
    
    52        // within this contract and contracts that derive this contract
    
    53        // Learn more: https://solidity.readthedocs.io/en/v0.5.10/contracts.html#visibility-and-getters
    
    54        internal
    
    55        // `isUnique` is a function modifier that checks if the pizza already exists
    
    56        // Learn more: https://solidity.readthedocs.io/en/v0.5.10/structure-of-a-contract.html#function-modifiers
    
    57        isUnique(_name, _dna)
    
    58    {
    
    59        // Adds Pizza to array of Pizzas and get id
    
    60        uint256 id = SafeMath.sub(pizzas.push(Pizza(_name, _dna)), 1);
    
    61
    
    62        // Checks that Pizza owner is the same as current user
    
    63        // Learn more: https://solidity.readthedocs.io/en/v0.5.10/control-structures.html#error-handling-assert-require-revert-and-exceptions
    
    64
    
    65        // note that address(0) is the zero address,
    
    66        // indicating that pizza[id] is not yet allocated to a particular user.
    
    67
    
    68        assert(pizzaToOwner[id] == address(0));
    
    69
    
    70        // Maps the Pizza to the owner
    
    71        pizzaToOwner[id] = msg.sender;
    
    72        ownerPizzaCount[msg.sender] = SafeMath.add(
    
    73            ownerPizzaCount[msg.sender],
    
    74            1
    
    75        );
    
    76    }
    
    77
    
    78    // Creates a random Pizza from string (name)
    
    79    function createRandomPizza(string memory _name) public {
    
    80        uint256 randDna = generateRandomDna(_name, msg.sender);
    
    81        _createPizza(_name, randDna);
    
    82    }
    
    83
    
    84    // Generates random DNA from string (name) and address of the owner (creator)
    
    85    function generateRandomDna(string memory _str, address _owner)
    
    86        public
    
    87        // Functions marked as `pure` promise not to read from or modify the state
    
    88        // Learn more: https://solidity.readthedocs.io/en/v0.5.10/contracts.html#pure-functions
    
    89        pure
    
    90        returns (uint256)
    
    91    {
    
    92        // Generates random uint from string (name) + address (owner)
    
    93        uint256 rand = uint256(keccak256(abi.encodePacked(_str))) +
    
    94            uint256(_owner);
    
    95        rand = rand % dnaModulus;
    
    96        return rand;
    
    97    }
    
    98
    
    99    // Returns array of Pizzas found by owner
    
    100    function getPizzasByOwner(address _owner)
    
    101        public
    
    102        // Functions marked as `view` promise not to modify state
    
    103        // Learn more: https://solidity.readthedocs.io/en/v0.5.10/contracts.html#view-functions
    
    104        view
    
    105        returns (uint256[] memory)
    
    106    {
    
    107        // Uses the `memory` storage location to store values only for the
    
    108        // lifecycle of this function call.
    
    109        // Learn more: https://solidity.readthedocs.io/en/v0.5.10/introduction-to-smart-contracts.html#storage-memory-and-the-stack
    
    110        uint256[] memory result = new uint256[](ownerPizzaCount[_owner]);
    
    111        uint256 counter = 0;
    
    112        for (uint256 i = 0; i < pizzas.length; i++) {
    
    113            if (pizzaToOwner[i] == _owner) {
    
    114                result[counter] = i;
    
    115                counter++;
    
    116            }
    
    117        }
    
    118        return result;
    
    119    }
    
    120
    
    121    // Transfers Pizza and ownership to other address
    
    122    function transferFrom(address _from, address _to, uint256 _pizzaId) public {
    
    123        require(_from != address(0) && _to != address(0), "Invalid address.");
    
    124        require(_exists(_pizzaId), "Pizza does not exist.");
    
    125        require(_from != _to, "Cannot transfer to the same address.");
    
    126        require(_isApprovedOrOwner(msg.sender, _pizzaId), "Address is not approved.");
    
    127
    
    128        ownerPizzaCount[_to] = SafeMath.add(ownerPizzaCount[_to], 1);
    
    129        ownerPizzaCount[_from] = SafeMath.sub(ownerPizzaCount[_from], 1);
    
    130        pizzaToOwner[_pizzaId] = _to;
    
    131
    
    132        // Emits event defined in the imported IERC721 contract
    
    133        emit Transfer(_from, _to, _pizzaId);
    
    134        _clearApproval(_to, _pizzaId);
    
    135    }
    
    136
    
    137    /**
    
    138     * Safely transfers the ownership of a given token ID to another address
    
    139     * If the target address is a contract, it must implement `onERC721Received`,
    
    140     * which is called upon a safe transfer, and return the magic value
    
    141     * `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`;
    
    142     * otherwise, the transfer is reverted.
    
    143    */
    
    144    function safeTransferFrom(address from, address to, uint256 pizzaId)
    
    145        public
    
    146    {
    
    147        // solium-disable-next-line arg-overflow
    
    148        this.safeTransferFrom(from, to, pizzaId, "");
    
    149    }
    
    150
    
    151    /**
    
    152     * Safely transfers the ownership of a given token ID to another address
    
    153     * If the target address is a contract, it must implement `onERC721Received`,
    
    154     * which is called upon a safe transfer, and return the magic value
    
    155     * `bytes4(keccak256("onERC721Received(address,address,uint256,bytes)"))`;
    
    156     * otherwise, the transfer is reverted.
    
    157     */
    
    158    function safeTransferFrom(
    
    159        address from,
    
    160        address to,
    
    161        uint256 pizzaId,
    
    162        bytes memory _data
    
    163    ) public {
    
    164        this.transferFrom(from, to, pizzaId);
    
    165        require(_checkOnERC721Received(from, to, pizzaId, _data), "Must implement onERC721Received.");
    
    166    }
    
    167
    
    168    /**
    
    169     * Internal function to invoke `onERC721Received` on a target address
    
    170     * The call is not executed if the target address is not a contract
    
    171     */
    
    172    function _checkOnERC721Received(
    
    173        address from,
    
    174        address to,
    
    175        uint256 pizzaId,
    
    176        bytes memory _data
    
    177    ) internal returns (bool) {
    
    178        if (!isContract(to)) {
    
    179            return true;
    
    180        }
    
    181
    
    182        bytes4 retval = IERC721Receiver(to).onERC721Received(
    
    183            msg.sender,
    
    184            from,
    
    185            pizzaId,
    
    186            _data
    
    187        );
    
    188        return (retval == _ERC721_RECEIVED);
    
    189    }
    
    190
    
    191    // Burns a Pizza - destroys Token completely
    
    192    // The `external` function modifier means this function is
    
    193    // part of the contract interface and other contracts can call it
    
    194    function burn(uint256 _pizzaId) external {
    
    195        require(msg.sender != address(0), "Invalid address.");
    
    196        require(_exists(_pizzaId), "Pizza does not exist.");
    
    197        require(_isApprovedOrOwner(msg.sender, _pizzaId), "Address is not approved.");
    
    198
    
    199        ownerPizzaCount[msg.sender] = SafeMath.sub(
    
    200            ownerPizzaCount[msg.sender],
    
    201            1
    
    202        );
    
    203        pizzaToOwner[_pizzaId] = address(0);
    
    204    }
    
    205
    
    206    // Returns count of Pizzas by address
    
    207    function balanceOf(address _owner) public view returns (uint256 _balance) {
    
    208        return ownerPizzaCount[_owner];
    
    209    }
    
    210
    
    211    // Returns owner of the Pizza found by id
    
    212    function ownerOf(uint256 _pizzaId) public view returns (address _owner) {
    
    213        address owner = pizzaToOwner[_pizzaId];
    
    214        require(owner != address(0), "Invalid Pizza ID.");
    
    215        return owner;
    
    216    }
    
    217
    
    218    // Approves other address to transfer ownership of Pizza
    
    219    function approve(address _to, uint256 _pizzaId) public {
    
    220        require(msg.sender == pizzaToOwner[_pizzaId], "Must be the Pizza owner.");
    
    221        pizzaApprovals[_pizzaId] = _to;
    
    222        emit Approval(msg.sender, _to, _pizzaId);
    
    223    }
    
    224
    
    225    // Returns approved address for specific Pizza
    
    226    function getApproved(uint256 _pizzaId)
    
    227        public
    
    228        view
    
    229        returns (address operator)
    
    230    {
    
    231        require(_exists(_pizzaId), "Pizza does not exist.");
    
    232        return pizzaApprovals[_pizzaId];
    
    233    }
    
    234
    
    235    /**
    
    236     * Private function to clear current approval of a given token ID
    
    237     * Reverts if the given address is not indeed the owner of the token
    
    238     */
    
    239    function _clearApproval(address owner, uint256 _pizzaId) private {
    
    240        require(pizzaToOwner[_pizzaId] == owner, "Must be pizza owner.");
    
    241        require(_exists(_pizzaId), "Pizza does not exist.");
    
    242        if (pizzaApprovals[_pizzaId] != address(0)) {
    
    243            pizzaApprovals[_pizzaId] = address(0);
    
    244        }
    
    245    }
    
    246
    
    247    /*
    
    248     * Sets or unsets the approval of a given operator
    
    249     * An operator is allowed to transfer all tokens of the sender on their behalf
    
    250     */
    
    251    function setApprovalForAll(address to, bool approved) public {
    
    252        require(to != msg.sender, "Cannot approve own address");
    
    253        operatorApprovals[msg.sender][to] = approved;
    
    254        emit ApprovalForAll(msg.sender, to, approved);
    
    255    }
    
    256
    
    257    // Tells whether an operator is approved by a given owner
    
    258    function isApprovedForAll(address owner, address operator)
    
    259        public
    
    260        view
    
    261        returns (bool)
    
    262    {
    
    263        return operatorApprovals[owner][operator];
    
    264    }
    
    265
    
    266    // Takes ownership of Pizza - only for approved users
    
    267    function takeOwnership(uint256 _pizzaId) public {
    
    268        require(_isApprovedOrOwner(msg.sender, _pizzaId), "Address is not approved.");
    
    269        address owner = this.ownerOf(_pizzaId);
    
    270        this.transferFrom(owner, msg.sender, _pizzaId);
    
    271    }
    
    272
    
    273    // Checks if Pizza exists
    
    274    function _exists(uint256 pizzaId) internal view returns (bool) {
    
    275        address owner = pizzaToOwner[pizzaId];
    
    276        return owner != address(0);
    
    277    }
    
    278
    
    279    // Checks if address is owner or is approved to transfer Pizza
    
    280    function _isApprovedOrOwner(address spender, uint256 pizzaId)
    
    281        internal
    
    282        view
    
    283        returns (bool)
    
    284    {
    
    285        address owner = pizzaToOwner[pizzaId];
    
    286        // Disable solium check because of
    
    287        // https://github.com/duaraghav8/Solium/issues/175
    
    288        // solium-disable-next-line operator-whitespace
    
    289        return (spender == owner ||
    
    290            this.getApproved(pizzaId) == spender ||
    
    291            this.isApprovedForAll(owner, spender));
    
    292    }
    
    293
    
    294    // Check if Pizza is unique and doesn't exist yet
    
    295    modifier isUnique(string memory _name, uint256 _dna) {
    
    296        bool result = true;
    
    297        for (uint256 i = 0; i < pizzas.length; i++) {
    
    298            if (
    
    299                keccak256(abi.encodePacked(pizzas[i].name)) ==
    
    300                keccak256(abi.encodePacked(_name)) &&
    
    301                pizzas[i].dna == _dna
    
    302            ) {
    
    303                result = false;
    
    304            }
    
    305        }
    
    306        require(result, "Pizza with such name already exists.");
    
    307        _;
    
    308    }
    
    309
    
    310    // Returns whether the target address is a contract
    
    311    function isContract(address account) internal view returns (bool) {
    
    312        uint256 size;
    
    313        // Currently there is no better way to check if there is a contract in an address
    
    314        // than to check the size of the code at that address.
    
    315        // See https://ethereum.stackexchange.com/a/14016/36603
    
    316        // for more details about how this works.
    
    317        // TODO Check this again before the Serenity release, because all addresses will be
    
    318        // contracts then.
    
    319        // solium-disable-next-line security/no-inline-assembly
    
    320        assembly {
    
    321            size := extcodesize(account)
    
    322        }
    
    323        return size > 0;
    
    324    }
    
    325}
    
    Show all
    
    ![ðŸ“‹](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f4cb.svg) Copy

## Further reading

Check out Solidity and Vyper's documentation for a more complete overview of
smart contracts:

  * [Solidity(opens in a new tab)](https://solidity.readthedocs.io/)
  * [Vyper(opens in a new tab)](https://vyper.readthedocs.io/)

## Related topics

  * [Smart contracts](/en/developers/docs/smart-contracts/)
  * [Ethereum Virtual Machine](/en/developers/docs/evm/)

## Related tutorials

  * [Downsizing contracts to fight the contract size limit](/en/developers/tutorials/downsizing-contracts-to-fight-the-contract-size-limit/) _â€“ Some practical tips for reducing the size of your smart contract._
  * [Logging data from smart contracts with events](/en/developers/tutorials/logging-events-smart-contracts/) _â€“ An introduction to smart contract events and how you can use them to log data._
  * [Interact with other contracts from Solidity](/en/developers/tutorials/interact-with-other-contracts-from-solidity/) _â€“ How to deploy a smart contract from an existing contract and interact with it._

back-to-top â†‘

### Was this article helpful?

YesNo

![ðŸ‘ˆ](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f448.svg)

Previous

[Smart contract languages](/en/developers/docs/smart-contracts/languages/)

![ðŸ‘‰](https://cdnjs.cloudflare.com/ajax/libs/twemoji/12.0.4/2/svg/1f449.svg)

Next

[Smart contracts libraries](/en/developers/docs/smart-contracts/libraries/)

  * [Edit page(opens in a new tab)](https://github.com/ethereum/ethereum-org-website/tree/dev/public/content/developers/docs/smart-contracts/anatomy/index.md)
  * On this page

    * Prerequisites
    * Data
      * Storage
      * Memory
      * Environment variables
    * Functions
      * View functions
      * Constructor functions
      * Built-in functions
    * Writing functions
    * Events and logs
    * Annotated examples
      * Hello world
      * Token
      * Unique digital asset
    * Further reading
    * Related topics
    * Related tutorials

Website last updated: May 22, 2024

[(opens in a new tab)](https://github.com/ethereum/ethereum-org-
website)[(opens in a new tab)](https://twitter.com/ethdotorg)[(opens in a new
tab)](https://discord.gg/ethereum-org)

### Learn

  * [Learn Hub](/en/learn/)
  * [What is Ethereum?](/en/what-is-ethereum/)
  * [What is ether (ETH)?](/en/eth/)
  * [Ethereum wallets](/en/wallets/)
  * [What is Web3?](/en/web3/)
  * [Smart contracts](/en/smart-contracts/)
  * [Gas fees](/en/gas/)
  * [Run a node](/en/run-a-node/)
  * [Ethereum security and scam prevention](/en/security/)
  * [Quiz Hub](/en/quizzes/)
  * [Ethereum glossary](/en/glossary/)

### Use

  * [Guides](/en/guides/)
  * [Choose your wallet](/en/wallets/find-wallet/)
  * [Get ETH](/en/get-eth/)
  * [Dapps - Decentralized applications](/en/dapps/)
  * [Stablecoins](/en/stablecoins/)
  * [NFTs - Non-fungible tokens](/en/nft/)
  * [DeFi - Decentralized finance](/en/defi/)
  * [DAOs - Decentralized autonomous organizations](/en/dao/)
  * [Decentralized identity](/en/decentralized-identity/)
  * [Stake ETH](/en/staking/)
  * [Layer 2](/en/layer-2/)

### Build

  * [Builder's home](/en/developers/)
  * [Tutorials](/en/developers/tutorials/)
  * [Documentation](/en/developers/docs/)
  * [Learn by coding](/en/developers/learning-tools/)
  * [Set up local environment](/en/developers/local-environment/)
  * [Grants](/en/community/grants/)
  * [Foundational topics](/en/developers/docs/intro-to-ethereum/)
  * [UX/UI design fundamentals](/en/developers/docs/design-and-ux/)
  * [Enterprise - Mainnet Ethereum](/en/enterprise/)
  * [Enterprise - Private Ethereum](/en/enterprise/private-ethereum/)

### Participate

  * [Community hub](/en/community/)
  * [Online communities](/en/community/online/)
  * [Ethereum events](/en/community/events/)
  * [Contributing to ethereum.org](/en/contributing/)
  * [Translation Program](/en/contributing/translation-program/)
  * [Ethereum bug bounty program](/en/bug-bounty/)
  * [Ethereum Foundation](/en/foundation/)
  * [Ethereum Foundation Blog(opens in a new tab)](https://blog.ethereum.org/)
  * [Ecosystem Support Program(opens in a new tab)](https://esp.ethereum.foundation)
  * [Devcon(opens in a new tab)](https://devcon.org/)

### Research

  * [Ethereum Whitepaper](/en/whitepaper/)
  * [Ethereum roadmap](/en/roadmap/)
  * [Improved security](/en/roadmap/security/)
  * [Technical history of Ethereum](/en/history/)
  * [Open research](/en/community/research/)
  * [Ethereum Improvement Proposals](/en/eips/)
  * [Ethereum governance](/en/governance/)

  * [About us](/en/about/)
  * [Ethereum brand assets](/en/assets/)
  * [Code of conduct](/en/community/code-of-conduct/)
  * [Jobs](/en/about/#open-jobs)
  * [Privacy policy](/en/privacy-policy/)
  * [Terms of use](/en/terms-of-use/)
  * [Cookie policy](/en/cookie-policy/)
  * [Press Contact(opens in a new tab)](mailto:press@ethereum.org)

Is this page helpful?

